property set MAP_Error_Properties is
with EMV2, MAP_Errors;

-- ====================================== --
-- Types related to the report background --
-- ====================================== --

Context : type aadlstring;

Assumption : type aadlstring;

Abbreviation : type record (
	Full : aadlstring;
	Definition : aadlstring;
);

-- =================================== --
-- Types related to STPA preliminaries --
-- =================================== --

Accident_Level : type record (
	Level : aadlinteger;
	Description: aadlstring;
	Explanations : list of aadlstring;
);

Accident : type record (
	Description : aadlstring;
	Level : MAP_Error_Properties::Accident_Level;
	Explanations : list of aadlstring;
);

Hazard: type record (
	Description : aadlstring;
	Accident : MAP_Error_Properties::Accident;
	HazardousFactor : aadlstring;
	SystemElement : reference (device, process);
	SystemElementState : aadlstring;
	EnvironmentElement : reference (abstract);
	EnvironmentElementState : aadlstring;
	Explanations : list of aadlstring;
);

Constraint: type record (
	Description : aadlstring;
	Hazard : MAP_Error_Properties::Hazard;
	HazardousFactor : aadlstring;
	SystemElement : reference (device, process);
	SystemElementState : aadlstring;
	EnvironmentElement : reference (abstract);
	EnvironmentElementState : aadlstring;
	Explanations : list of aadlstring;
);

-- ============================ --
-- Types related to STPA step 1 --
-- ============================ --

KindsType : type enumeration (
	NotProviding, Providing, Early, Late, AppliedTooLong, StoppedTooSoon, 
	ValueLow, ValueHigh, ParamsMissing, ParamsWrong, ParamsOutOfOrder
);

Occurrence : record (
	Kind : MAP_Error_Properties::KindsType;
	ViolatedConstraint : MAP_Error_Properties::Constraint;
	Title : aadlstring;
	Cause : record (
		ErrorType : reference ({emv2}**error type);
		Description : aadlstring;
	);
	Compensation : aadlstring;
) applies to (port connection, {emv2}**connection error source);

ManifestationType : type enumeration(
	Content, ContentHigh, ContentLow,
	Halted,
	Erratic,
	TimingEarly, TimingLate,
	ClearText
);

Manifestation : MAP_Error_Properties::ManifestationType applies to ({emv2}**error propagation);

end MAP_Error_Properties;